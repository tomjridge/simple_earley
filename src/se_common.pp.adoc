
== Common definitions, all versions (file: se_common)

Various types and definitions


FIXME move

// FIXME math rendering nicer with something like dollar{"xxx".m}? yes; change the below

We work with `nt_items` only (terminal parses are not cached). No
staging. +++\(O(n^3)\)+++.

We define the map types that we need. From a set of complete items, we
need to identify those for a given start index and symbol. This is the
role of the complete map type `cm_t`. The codomain of the map is a set
of integers +++\(j\)+++. If +++\(j \in cm(k,S)\)+++ then there is a
complete item latexmath:[( {}_{k} S_{j})].

The blocked map allows us to identify, from a set of nt items, those
that are currently blocked at position +++\(k\)+++ waiting for a parse
of symbol +++\(S\)+++ to complete. +++\(nitm \in bm(k,S)\)+++ if
+++\(nitm\)+++ is of the form latexmath:[( X \rightarrow {}_{i} \alpha {}_{k} . S \beta)].


=== Utility functions

[source,ocaml]
----

let dest_Some = function Some x -> x | _ -> (failwith "dest_Some")

let find_with_default d f k m = try (f k m) with Not_found -> d

let rec while_not_nil': 'a list -> 's -> ('s -> 'a -> 's) -> 's = (
    fun xs s0 f -> List.fold_left f s0 xs)

let rec while_not_nil: 'a list -> ('a -> 's -> 's) -> 's -> 's = (
    fun xs f s0 -> List.fold_left (fun x y -> f y x) s0 xs)


----


// ----------------------------------------
=== Common type definitions, all versions 

Indexes into the input

[source,ocaml]
----

type k_t = int
type i_t = int
type j_t = int

----

Nonterminals, terminals and symbols 

[source,ocaml]
----

type nt = int
type tm = int
type sym = NT of nt | TM of tm

----

Items. Typically we only use `nt_item`, but the other items appear eg in `se_spec_all_items`.

[source,ocaml]
----

type nt_item = {
    nt: nt;
    i: i_t;
    as_: sym list;
    k: k_t;
    bs: sym list
  }

type tm_item = {
    k: k_t;
    tm: tm
  }

type sym_item = {
    k: k_t;
    sym: sym
  }

type citm_t = {
    k: k_t;
    sym: sym;
    j: j_t 
  }

----

Strings and substrings. We make the string type abstract to illustrate
that we don't depend on any properties of strings (except their
interaction with terminal parsers).

[source,ocaml]
----

type string_t
type substring_t = (string_t * i_t * j_t)

let string_to_string_t: string -> string_t = (fun s -> Obj.magic s)
let string_t_to_string: string_t -> string = (fun s -> Obj.magic s)

----

The grammar type, as a function, and the terminal parsers.

[source,ocaml]
----
type grammar_t = {
    nt_items_for_nt: nt -> (string_t * int) -> nt_item list;
    p_of_tm: tm -> substring_t -> k_t list
  }

----

The input is a string. Since `string_t` is abstract, we also need the length.

[source,ocaml]
----
type input_t = {
    str: string_t;
    len: int;
  }

----

During a parse, we need the following context.

[source,ocaml]
----
type ctxt_t = {
    g0: grammar_t;
    i0: input_t
  }

----

The core Earley step involves cutting a blocked item against a complete item.

[source,ocaml]
----


let cut: nt_item -> j_t -> nt_item = (
    fun bitm j0 -> (
      let as_ = (List.hd bitm.bs)::bitm.as_ in
      let bs = List.tl bitm.bs in
      let k = j0 in
      let nitm ={bitm with k;as_;bs} in
      nitm ))

----



// ----------------------------------------
=== Blocked and complete maps? 

[source,ocaml]
----

module Unstaged = struct

  type bitm_t = nt_item  (* bs <> [] *)

  type b_key_t = (k_t * sym)

  let bitm_to_key: bitm_t -> b_key_t = (
      fun bitm -> (bitm.k,List.hd bitm.bs))

  type c_key_t = (k_t * sym)
               
  let citm_to_key: citm_t -> c_key_t = (
      fun citm -> (citm.k,citm.sym))

end

----


// ----------------------------------------
=== Various sets and maps 

[source,ocaml]
----

let comp = Pervasives.compare

module Int_set = 
  Set.Make(struct type t = int;; let compare: t -> t -> int = comp end)

module Nt_item_set = 
  Set.Make(struct type t = nt_item;; let compare: t -> t -> int = comp end)

module Sym_item_set = 
  Set.Make(struct type t = sym_item;; let compare: t -> t -> int = comp end)

module Nt_set = 
  Set.Make(struct type t = nt;; let compare: t -> t -> int = comp end)

module Map_tm =
  Map.Make(struct type t = tm;; let compare: t -> t -> int = comp end)

module Map_nt =
  Map.Make(struct type t = nt;; let compare: t -> t -> int = comp end)

module Map_int = 
  Map.Make(struct type t = int;; let compare: t -> t -> int = comp end)

----



// ----------------------------------------
=== Debug support 

[source,ocaml]
----

let debug = ref false

let debug_endline = (
    fun x -> 
    if !debug then print_endline x else ())

let sym_to_string s = (match s with | NT nt -> Printf.sprintf "NT %d" nt | TM tm -> Printf.sprintf "TM %d" tm)

let sym_list_to_string ss = (ss |> List.map sym_to_string |> String.concat "," |> fun x -> "["^x^"]")

let nitm_to_string nitm = (
    Printf.sprintf "(%d %d %s %d %s)" 
                   nitm.nt nitm.i 
                   (sym_list_to_string nitm.as_) 
                   nitm.k 
                   (sym_list_to_string nitm.bs))

----
